plugins {
    id 'java'
    id 'org.hidetake.swagger.generator' version '2.19.2'
}

group 'com.intuit.feedback'
version '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':domain')
    implementation project(':usecases')
    implementation "org.springframework.boot:spring-boot-starter-data-mongodb-reactive:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-validation:${springBootVersion}"
    implementation 'javax.validation:validation-api:2.0.1.Final'
    implementation group: 'io.swagger', name: 'swagger-annotations', version: '1.6.10'
    implementation "com.bol:spring-data-mongodb-encrypt:${springBootDataMongoDBEncrypt}"
    implementation "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonDatabindVersion}"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:${jacksonVersion}"
    implementation "org.projectlombok:lombok:${lombokVersion}"
    implementation "net.rakugakibox.spring.boot:orika-spring-boot-starter:${orikaVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    swaggerCodegen 'io.swagger:swagger-codegen-cli:2.4.31'
}

swaggerSources {
    accountManagement {
        inputFile = file('swagger/user-management-api-spec.yml')
        code {
            language = 'spring'
            configFile = file('swagger/config.json')
            components = ['models']
        }
    }
}

// Configure compile task dependency and source
compileJava.dependsOn swaggerSources.accountManagement.code
sourceSets.main.java.srcDir "${swaggerSources.accountManagement.code.outputDir}/src/main/java"
sourceSets.main.resources.srcDir "${swaggerSources.accountManagement.code.outputDir}/src/main/resources"

test {
    useJUnitPlatform()
}
